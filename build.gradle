plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'org.beryx.jlink' version '2.21.4'
}

group 'org.innopolisU'
sourceCompatibility = 11
mainClassName = 'com.application.AppLauncher'
project.description = 'Simple application for collecting data in linux'
project.ext.buildDate = new Date()
project.version = '3.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'
    implementation group: 'org.json', name: 'json', version: '20090211'

    implementation "net.java.dev.jna:jna:5.5.0"
    implementation "net.java.dev.jna:jna-platform:5.5.0"
    implementation 'org.xerial:sqlite-jdbc:3.31.1'

    testImplementation "org.testfx:testfx-core:4.0.+"
    testImplementation "org.testfx:testfx-junit:4.0.+"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

    implementation 'com.dorkbox:SystemTray:3.17'
}

javafx{
    modules = ['javafx.controls', 'javafx.swing', 'javafx.fxml', 'javafx.media',  'javafx.graphics']
    version = '12'
}

task copyLicense {
    outputs.file new File("$buildDir/LICENSE")
    doLast {
        copy {
            from "LICENSE"
            into "$buildDir"
        }
    }
}

jar {
    manifest {
        attributes('Main-Class': "$mainClassName")
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    targetPlatform('linux', '/usr/lib/jvm/adoptopenjdk-11-hotspot-amd64')
    launcher {
        name = 'DataCollectorLinux'
    }
    addExtraDependencies("javafx")
    jpackage {
        jpackageHome = '/usr/lib/jvm/java-14-openjdk-amd64'
        skipInstaller = false
        installerType = 'deb'
        imageOptions = ['--icon', 'src/main/resources/metrics-collector.png']
        installerOptions = ['--description', project.description, '--vendor', 'Innopolis University', '--app-version', project.version, '--linux-shortcut',
                            '--linux-deb-maintainer', 'innometrics@innopolis.ru']
    }
}

tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
}
tasks.jpackageImage.doLast {
    copy {
        from "src/main/resources"
        //include "splash.png"
        //into "build/jpackage/$project.name/app"
        into "build/jpackage/DataCollectorLinux/lib/app"
    }
}

test {
    useJUnitPlatform()
    //minHeapSize = "128m"
    //maxHeapSize = "3g"
    //testLogging.showStandardStreams = true
}